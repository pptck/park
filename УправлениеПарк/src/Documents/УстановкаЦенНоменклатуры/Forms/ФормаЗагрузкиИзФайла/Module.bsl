
#Область ОписаниеПеременных

#КонецОбласти

#Область ОбработчикиСобытийФормы

// Код процедур и функций


&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ОбработатьФайлЦен(Параметры.АдресФайла);
	
	ПодобратьНоменклатуру();
	
КонецПроцедуры
#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

// Код процедур и функций

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПеренестиВДокумент(Команда)
	
	Адрес = ПоместитьТаблицу();
	
	Закрыть(Адрес);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОбработатьФайлЦен(Адрес)
	
	ДвоичныеДанные = ПолучитьИзВременногоХранилища(Адрес);
	ИмяФайла = ПолучитьИмяВременногоФайла("xlsx");
	
	ДвоичныеДанные.Записать(ИмяФайла);

	ТабличныйДокумент = Новый ТабличныйДокумент();
	ТабличныйДокумент.Прочитать(ИмяФайла);
	
	Для НомерСтроки = 2 по ТабличныйДокумент.ВысотаТаблицы Цикл
		
		Наименование = ТабличныйДокумент.Область(НомерСтроки, 1).Текст;
		Цена = ТабличныйДокумент.Область(НомерСтроки, 2).Текст;		
		
		Строка = ДанныеФайла.Добавить();
		Строка.Наименование = Наименование;
		Строка.Цена = Число(Цена);
	КонецЦикла;
	
	УдалитьФайлы(ИмяФайла);
	
КонецПроцедуры

&НаСервере
Процедура ПодобратьНоменклатуру()
	Запрос = Новый Запрос();
	Запрос.Текст = 
		"Выбрать
		|	Т.Наименование КАК Наименование,
		|	Т.Цена КАК Цена
		|ПОМЕСТИТЬ ВТ_ДанныеФайла
		|Из
		|	&ДанныеФайла КАК Т
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_ДанныеФайла.Наименование,
		|	МАКСИМУМ(ВТ_ДанныеФайла.Цена) КАК Цена,
		|	Номенклатура.Ссылка КАК Номенклатура
		|ИЗ
		|	ВТ_ДанныеФайла КАК ВТ_ДанныеФайла
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК Номенклатура
		|		ПО ВТ_ДанныеФайла.Наименование = Номенклатура.Наименование
		|		И НЕ Номенклатура.ПометкаУдаления
		|СГРУППИРОВАТЬ ПО
		|	ВТ_ДанныеФайла.Наименование,
		|	Номенклатура.Ссылка";
		
	Запрос.УстановитьПараметр("ДанныеФайла",ДанныеФайла.Выгрузить());
	
	ДанныеФайла.Загрузить(Запрос.Выполнить().Выгрузить());
	
	
КонецПроцедуры

&НаСервере
Функция ПоместитьТаблицу()
	
	Возврат ПоместитьВоВременноеХранилище(ДанныеФайла.Выгрузить(,"Номенклатура, Цена"));
	
КонецФункции 
	

#КонецОбласти
